---
title: 菲涅尔反射
tags:
  - Shader
  - Shader基础
  - 高级纹理
  - 立方体纹理
categories:
  - [技术美术, UnityShader, 高级纹理]
author:
  - nightstardawn
---

# 菲涅尔反射

## 一、菲涅尔反射是什么

一种光学效果
实现垂直于表面观察，反射较弱，而当实现非垂直于表面观察，夹角越小，反射越明显

## 二、菲涅尔反射效果在 UnityShader 中的作用

在 Unity 中，使用菲涅尔反射比直接使用反射能更加贴近真实世界的效果，在实现一些金属感、陶瓷感、玻璃感时效果更好

## 三、菲涅尔反射实现效果的原理

菲涅尔反射的原理还是利用立方体纹理进行环境映射
我们在计算反射时，还是利用之前的那一套反射计算规则
只是为我们在决定采样颜色时，需要使用的反射率变量添加新的计算规则

反射率使用菲涅尔等式来计算
但是在物理学中，菲涅尔等式非常复杂
所以在 UnityShader 中我们使用**Schlick 菲尼尔近似等式**来计算
`R(θ) = R0 + (1 - R0 )(1 - cos(θ))⁵`
⬇
`R(θ) = R0 + (1 - R0 )(1 - V·N)⁵`

- R(θ) 表示入射角为 θ 时的反射率
- R0 表示垂直入射时某介质的反射率
- V 表示视角方向的单位向量
- N 表示顶点法向量

## 四、菲尼尔反射的基本实现

### 1.实现思路

1. 在普通反射的基础上进行修改，将普通的反射率通过为菲涅尔近似等式计算菲涅尔反射率
2. 属性声明
   将反射率变量修改为 \_Fresnel 菲涅尔反射中的介质反射率
3. 结构体
   添加
   1. 视角方向
   2. 法线方向
4. 顶点着色器
   结构体中变量的赋值
5. 片元着色器
   通过 Schlick 菲涅尔近似公式(`R(θ) = R0 + (1 - R0 )(1 - V·N)⁵`)
   计算菲尼尔反射率

### 2.实现示例

```cs
Shader "Unlit/FresnelBase"
{
     Properties
    {
        //立方体纹理
        _Cube("Cubemap",Cube) = ""{}
        //菲涅尔反射中对应介质的反射率
        _FresnelScale("FresnelBase",Range(0,1)) = 1
    }
    SubShader
    {
        Tags{"RenderType" = "Opaque" "Queue" = "Geometry"}

        Pass
        {
            Tags{"LightMode" = "ForwardBase"}
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #include "Lighting.cginc"

            samplerCUBE _Cube;
            float _FresnelScale;

            struct v2f
            {
                float4 pos:SV_POSITION;//裁剪坐标下的顶点坐标
                //世界空间下的法线向量
                float3 worldNormal:NORMAL;
                float3 worldViewDir:TEXCOORD0;
                //世界空间下的顶点坐标
                //我们将把反射向量的的计算放在顶点坐标系下 节约性能 表现效果肉眼几乎和片元着色器一致
                float3 worldRefl:TEXCOORD1;

            };
            v2f vert(appdata_base v)
            {
                v2f o;
                //顶点坐标转裁剪坐标
                o.pos = UnityObjectToClipPos(v.vertex);
                //计算计算反射光向量
                //1.计算世界空间下的法线向量
                o.worldNormal = UnityObjectToWorldNormal(v.normal);
                //2.计算世界空间下的顶点坐标
                fixed3 worldPos = mul(unity_ObjectToWorld,v.vertex).xyz;
                //3.计算视角方向 内部是用摄像机位置-世界坐标位置
                o.worldViewDir = UnityWorldSpaceViewDir(worldPos);
                //计算反射向量
                o.worldRefl = reflect(-o.worldViewDir,o.worldNormal);
                return o;
            }
            fixed4 frag(v2f i):SV_TARGET
            {
                //对立方体纹理利用相应的反射函数进行采样
                fixed4 cubemapColor = texCUBE(_Cube,i.worldRefl);
                //通过菲尼尔定律计算反射率
                fixed fresenl = _FresnelScale + (1 - _FresnelScale)*pow(1-dot(normalize(i.worldNormal),normalize(i.worldViewDir)),5);
                //用采样颜色*反射率 得到最终效果
                return cubemapColor * fresenl;
            }
            ENDCG
        }
    }
}

```
